% Common hacky declarations
Kind   numidx   type.
Type   z        numidx.
Type   s        numidx -> numidx.

%%%%%%%%%%%%%%
% Generation %
%%%%%%%%%%%%%%

% Domain-specific declarations. In particular, a generation strategy must be
% specified, here in the form of a bound that determines the exploration
% strategy for the state space. We envision two basic strategies: the height of
% the derivation tree and the size of the derivation (i.e., the number of term
% constructors utilized).
%
% Bounds are given using the inductive definition of natural numbers in numidx,
% because one hack is enough, and nat requires external definitions for
% whichever arithmetic operations used throughout.
Kind   qbound    type.
Type   qheight   numidx -> qbound.
Type   qsize     numidx -> numidx -> qbound.

Type   qgen   qbound -> cert.

%%%%%%%%%%%%
% Checking %
%%%%%%%%%%%%

Type   qsearch   cert.

%%%%%%%%%%%
% Staging %
%%%%%%%%%%%

% Trivial, single-branch staging. Currently a simple wrapper on a pair of
% certificates assumed to cover the generation (qgen) and checking (qsearch)
% stages, respectively. Exhaustive search does not require a "number of tries",
% since the generation stage determines exactly the search space to be
% inspected.
Type   qstart   cert -> cert -> cert.

%%%%%%%%%%%%
% Indexing %
%%%%%%%%%%%%

Type   qidx   idx.

%%%%%%%%%%%
% Helpers %
%%%%%%%%%%%

% Nothing here at the moment.

%%%%%%%%%%%%%%%%%%%%%%
% Clerks and experts %
%%%%%%%%%%%%%%%%%%%%%%

% Quick and dirty reuse.
%#include "debug-simple-fpc.thm". %DEBUG

Define unmarshal : cert -> cert -> prop by unmarshal C C.

% member is predefined.

Define releaseRExpert : cert -> cert -> prop by
releaseRExpert qsearch qsearch.

Define releaseLExpert : cert -> cert -> prop by
releaseLExpert X1? X2? := false.

Define decideRClerk : cert -> cert -> prop by
decideRClerk (qgen Bound) (qgen Bound) ;
%
decideRClerk (qstart Gen Check) (qstart Gen Check).

Define storeRClerk : cert -> cert -> prop by
storeRClerk (qgen Bound) (qgen Bound) ;
%
storeRClerk (qstart Gen Check) (qstart Gen Check).

Define decideLClerk' : cert -> cert -> idx -> prop by
decideLClerk' X1? X2? X3? := false.

Define decideLClerk : cert -> cert -> idx -> prop by
decideLClerk X1? X2? X3? := false.

Define storeLClerk : cert -> cert -> idx -> prop by
storeLClerk X1? X2? X3? := false.

Define initLExpert : cert -> prop by
initLExpert X? := false.

Define freezeRClerk : cert -> cert -> prop by
freezeRClerk X1? X2? := false.

Define initRExpert : cert -> idx -> prop by
initRExpert X1? X2? := false.

Define freezeLClerk : cert -> cert -> idx -> prop by
freezeLClerk X1? X2? X3? := false.

Define unfoldRClerk : cert -> cert -> prop by
unfoldRClerk X1? X2? := false.

Define unfoldLExpert : cert -> cert -> prop by
unfoldLExpert X1? X2? := false.

Define unfoldRExpert : cert -> cert -> prop by
unfoldRExpert (qgen (qheight (s Height))) (qgen (qheight Height)) ;
unfoldRExpert (qgen (qsize (s In) Out)  ) (qgen (qsize In Out)  ) ;
%
unfoldRExpert qsearch qsearch.

%NOTE Ensure termination!
Define unfoldLClerk : cert -> cert -> prop by
unfoldLClerk qsearch qsearch.

Define coindClerk' : cert -> ( i -> cert ) -> prop by
coindClerk' X1? X2? := false.

Define coindClerk : cert -> cert -> ( i -> cert ) -> ( i -> bool ) -> prop by
coindClerk X1? X2? X3? X4? := false.

Define indClerk' : cert -> ( i -> cert ) -> prop by
indClerk' X1? X2? := false.

Define indClerk : cert -> cert -> ( i -> cert ) -> ( i -> bool ) -> prop by
indClerk X1? X2? X3? X4? := false.

Define someExpert : cert -> cert -> i -> prop by
someExpert (qgen Bound) (qgen Bound) X ;
%
someExpert (qstart Gen Check) (qstart Gen Check) X ;
%
someExpert qsearch qsearch X.

Define allExpert : cert -> cert -> i -> prop by
allExpert X1? X2? X3? := false.

Define someClerk : cert -> ( i -> cert ) -> prop by
someClerk qsearch (x\ qsearch).

Define allClerk : cert -> ( i -> cert ) -> prop by
allClerk X1? X2? := false.

Define eqExpert : cert -> prop by
eqExpert (qgen (qheight X?) ) ;
eqExpert (qgen (qsize In In)) ;
%
eqExpert qsearch.

Define impExpert' : cert -> cert -> cert -> prop by
impExpert' X1? X2? X3? := false.

Define impExpert : cert -> cert -> cert -> prop by
impExpert X1? X2? X3? := false.

% Exhaustive exploration of the state space makes orExpert simple stuff.
Define orExpert : cert -> cert -> choice -> prop by
orExpert (qgen Bound) (qgen Bound) X ;
%
orExpert qsearch qsearch X.

Define andExpert : cert -> cert -> cert -> prop by
andExpert (qgen (qheight Height)) (qgen (qheight Height) ) (qgen (qheight Height)  ) ;
andExpert (qgen (qsize In Out)  ) (qgen (qsize In Middle)) (qgen (qsize Middle Out)) ;
%
andExpert qsearch qsearch qsearch ;
%
andExpert (qstart Gen Check) Gen Check.

Define ttExpert : cert -> prop by
ttExpert X? := false.

Define eqClerk : cert -> cert -> prop by
eqClerk qsearch qsearch.

Define impClerk : cert -> cert -> prop by
impClerk qsearch qsearch.

Define orClerk : cert -> cert -> cert -> prop by
orClerk qsearch qsearch qsearch.

Define andClerk : cert -> cert -> prop by
andClerk qsearch qsearch.

Define ttClerk : cert -> cert -> prop by
ttClerk X1? X2? := false.

Define ffClerk : cert -> prop by
ffClerk X? := false.
